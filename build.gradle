plugins {
	id 'org.springframework.boot' version '2.5.5'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id "org.asciidoctor.convert" version "1.5.9.2"
	id "org.flywaydb.flyway" version "8.0.1"
	id 'jacoco'
}

group = 'io.onezero'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

dependencies {
	asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
//	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.flywaydb:flyway-core'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'mysql:mysql-connector-java'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

ext {
	snippetsDir = file('build/generated-snippets')
}

test {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

asciidoctor {
	inputs.dir snippetsDir
	dependsOn test
}

bootJar {
	dependsOn asciidoctor
	from("${asciidoctor.outputDir}/html5") {
		into "BOOT-INF/classes/static/docs"
	}
}

task copyDocs(type: Copy) {
	dependsOn asciidoctor
	from file("build/asciidoc/html5")
	into file("src/main/resources/static/docs")
}

flyway {
	url = "jdbc:mysql://localhost:3306"
	schemas = ["jangpyunham"]
	user = "root"
	password = "secret"
	locations = ['classpath:db/migration']
}

flywayMigrate.dependsOn(classes)

build {
	dependsOn copyDocs
}